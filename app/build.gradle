plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.bugsnag.android.gradle'
}

String bugsnagApiKey = "NONE"
try {
    Properties props = new Properties()
    file("../secret.properties").withInputStream { props.load(it) }
    bugsnagApiKey = props.getProperty("BUGSNAG_API_KEY")
} catch (FileNotFoundException ignored) {
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.ridwanstandingby.particlelife"
        minSdk 21
        targetSdk 31
        versionCode 10
        versionName "1.7"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        all {
            manifestPlaceholders = [
                    bugsnagApiKey: "$bugsnagApiKey"
            ]
            buildConfigField("String", "BUGSNAG_API_KEY", "\"$bugsnagApiKey\"")
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
        freeCompilerArgs += ['-Xopt-in=kotlin.RequiresOptIn']
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$composeCompilerVersion"
    }
    lint {
        disable 'Instantiatable'
    }
}

dependencies {
    implementation project(path: ':verve')

    // Android
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'

    // Compose
    implementation "androidx.activity:activity-compose:$androidActivityComposeVersion"
    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.material:material:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation "androidx.compose.runtime:runtime:$composeVersion"
    implementation "androidx.compose.compiler:compiler:$composeCompilerVersion"
    implementation "androidx.compose.runtime:runtime-livedata:$composeVersion"

    // Koin
    implementation("io.insert-koin:koin-core:$koinVersion") {
        exclude group: 'org.jetbrains.kotlin', module: 'org.jetbrains.kotlin'
    }
    implementation("io.insert-koin:koin-android:$koinVersion") {
        exclude group: 'org.jetbrains.kotlin', module: 'org.jetbrains.kotlin'
    }
    implementation("io.insert-koin:koin-androidx-compose:$koinVersion") {
        exclude group: 'org.jetbrains.kotlin', module: 'org.jetbrains.kotlin'
    }

    // Logging
    implementation "com.bugsnag:bugsnag-android:$bugsnagAndroidVersion"
}